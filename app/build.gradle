apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 29

    defaultConfig {
        applicationId "net.pot8os.kotlintestsample"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }

    dataBinding {
        enabled = true
    }

    packagingOptions {
        exclude 'META-INF/proguard/androidx-annotations.pro'
    }

    // To run Android relevant tests under test dir.
    // See also http://robolectric.org/migrating/#project-configuration
    testOptions.unitTests.includeAndroidResources = true
}

ext {
    androidx_version = '1.1.0'
    test_version = '1.2.0'
    test_ext_version = '1.1.1'
    espresso_version = '3.2.0'
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "androidx.appcompat:appcompat:$androidx_version"
    implementation "androidx.annotation:annotation:$androidx_version"
    implementation "androidx.constraintlayout:constraintlayout:1.1.3"

    implementation "io.reactivex.rxjava2:rxandroid:2.1.0"
    implementation "io.reactivex.rxjava2:rxjava:2.2.3"
    implementation "com.jakewharton.rxbinding2:rxbinding-kotlin:2.2.0"
    implementation "io.reactivex.rxjava2:rxkotlin:2.2.0"

    testImplementation "junit:junit:4.12"
    testImplementation "androidx.test.ext:truth:$test_version"

    // Typical configurations for AndroidTest that runs under src/androidTest/
    androidTestImplementation "androidx.annotation:annotation:$androidx_version"
    androidTestImplementation "androidx.test:runner:$test_version"
    androidTestImplementation "androidx.test:rules:$test_version"
    androidTestImplementation "androidx.test.ext:junit:$test_ext_version"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso_version"

    // Thanks to Robolectric 4, we can run the test code for AndroidTest as unit tests.
    // This is for test/.../EmulatedApplicationTest.kt
    testImplementation 'org.robolectric:robolectric:4.3'
    testImplementation "androidx.annotation:annotation:$androidx_version"
    testImplementation "androidx.test:runner:$test_version"
    testImplementation "androidx.test:rules:$test_version"
    testImplementation "androidx.test.ext:junit:$test_ext_version"
    testImplementation "androidx.test.espresso:espresso-core:$espresso_version"
}

buildscript {
    ext.kotlin_version = '1.3.50'
    repositories {
        mavenCentral()

    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}
